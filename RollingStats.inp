function matrix rolling_ts (series y "Series of interest",
                            string which "Which statistics? Use gretl built-ins",
                            int wsize[1::2] "Size of the moving window",
                            bool recursive[0] "Recursive (expanding) window (default 0, rolling window)")

    # rolling_ts -- Rolling Time-series Statistics

    smpl y --no-missing
    scalar nsteps = nobs(y) - wsize + 1
    matrix m = zeros(nsteps,1)	# store results
    smpl $t1 ($t1+wsize-1)

    loop i=1..nsteps -q
        m[i] = @which	# call some built-in function
        if i<nsteps
            if recursive==1
                smpl ; +1
            else
                smpl +1 +1
            endif
        endif
    endloop

    /*
    # transform into series
    smpl y --no-missing --replace
    smpl ($t1+wsize-1) $t2
    series S = m
    return S
    */
    return m
end function


function series rvec2series (series y "Series of interest",
                            matrix m "Some vector of interest", 
                            int wsize[1::2] "Size of the moving window")
    # transform 'm' into series
    smpl y --no-missing --replace
    smpl ($t1+wsize-1) $t2
    if nobs(y) != rows(vec(m))
        printf "\nError: Vector 'm' has not the right length.\n"
        funcerr "Check whether the current sample is correctly specified."
    endif    
    series S = vec(m)
    setinfo S --description="rolling_ts() statistics"
    setinfo S --graph-name="rolling_ts() statistics"
    return S     
end function

function series rolling_ts_gui (series y "Select series of interest (y)",
                            string which "Compute: e.g 'mean(y)'",
                            int wsize[1::2] "Size of the moving window",
                            bool recursive[0] "Recursive window (default 'rolling')")
    
    series S = rvec2series(y, rolling_ts(y, which, wsize, recursive), wsize)
    printf "\n Series computed\n"
    return S
end function

/*
   function matrix skew (matrix X)
   # URL: http://de.mathworks.com/help/stats/skewness.html?requestedDomain=www.mathworks.com

   matrix xbar = X.-meanc(X)
   scalar A = 1/rows(X) .* sumc(xbar.^3)
   scalar B = ( sqrt(1/rows(X) .* sumc(xbar.^2) )).^3
   scalar S1 = A/B
   scalar S0a = sqrt(rows(X)*(rows(X)-1))
   scalar S0b = rows(X)-2
   S0 = S0a*S1/S0b				# bias-corrected formula, ==gretl's default

   return S0~S1
   end function
*/
